program = ws definition*:def ws operator*:op ws {VMProg(:def, :op)};

definition = ws "VAR" ws variable:v ws ":" ws type:t ws ";" ws {Def(:v, :t)};

type = ws "INT" ws {Int()} | ws "(" ws type:ind ws "ARRAY OF " ws type:t ws ")" ws {Array(:ind, :t)};

operator =  mark:m ws ":" ws variable:v ws ":=" ws expr:e ws "goto" ws set:s ws {Assgn(:m, :v, :e, :s)} |
			mark:m ws ":" ws "if" ws predicat:p ws "then" ws set:t ws "else" ws set:f ws {If(:m, :p, :t, :f)};

mark = (digit+)$d {Mark(s2i($d))};

set = "{" ws element*:m ws "}" ws {SetMark(:m)};
element = mark:m ws "," ws {:m} | mark:m ws {:m};

predicat =  "(" ws predicat:p1 ws logicOperator:lop ws predicat:p2 ws ")" ws {Pred(:p1, :lop, :p2)} |
			"(" ws expr:e1 ws cmpOperator:op ws expr:e2 ws ")" ws {Compare(:e1, :op, :e2)} |
			"(" ws "NOT" ws predicat:p ws ")" ws {Not(:p)} | "(" ws predicat:p ws ")" ws {:p} | "TRUE" ws {TRUE()} | "FALSE" ws {FALSE()};

expr = arifmExpr:e ws {:e} | funcExpr:e ws {:e} | const:c ws {:c} | variable:v ws {:v} | "(" ws expr:e ws ")" ws {:e};

arifmExpr = "(" ws expr:t "+" ws expr:p ")" ws {Sum(:t, :p)} |
			"(" ws expr:t "-" ws expr:p ")" ws {Sub(:t, :p)} |
			"(" ws expr:t "*" ws expr:p ")" ws {Mul(:t, :p)} |
			"(" ws expr:t "/" ws expr:p ")" ws {Div(:t, :p)};

funcExpr = "APP(" variable:arr ", " expr:ind ")" ws {APP(:arr, :ind)} | 
		   "UPD(" variable:arr ", " expr:ind ", " expr:val ")" ws {UPD(:arr, :ind, :val)};

cmpOperator = ("=" | "<=" | ">=" | "<" | ">" | "!=")$s ws {$s};
logicOperator = ("OR" | "AND")$s ws {$s};

variable = (('a' - 'z')+ digit*)$v ws {Var($v)}; 
const = ("-"? digit+)$c ws {Const(s2i($c))};
digit = '0' - '9';
ws = (" " | "\t" | "\n" | "\r")*;